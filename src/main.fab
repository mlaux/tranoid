// Entry point and title screen

charmap("0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ.,:?!\"'/()& \0", '\0', $d0)
: stows /strings

ct U[25] palette_initial = U[25](
    $2c, $27, $30,
    $0f, $0f, $0f, // make text black originally
    $0f, $0f, $0f,
    $0f, $0f, $0f,

    $0f, $0f, $0f,
    $0f, $0f, $0f,
    $0f, $0f, $0f,
    $0f, $0f, $0f,

    $0f // background color
)

ct U[] hidden_credit = U[] (
    $c0, $c1, $c2, $0, // NES
    $c3, $c9, $c4, $c5, $0, // PORT
    $c6, $c7, $0, // BY
    $c8, $c9, $c0, $c2, $c5, $c8, $ca, $c2, $c5, $0, // CONSTCAST
    $cb, $cc, $cb, $cd // 2025
)
ct UU HIDDEN_CREDIT_POS = $2063

data /rlz
    [] title_screen
        file(rlz, "title.nam")

vars /title
    UF scroll
    UF sprite_y
    UU frame = 0

mode main()
    goto mode title(true) : preserves // nothing

nmi title_nmi()
    if ready
        ppu_upload_oam_poll_pads(0)
        ppu_upload_palette()

        if pads[0].pressed & BUTTON_SELECT
            ppu_reset_addr(HIDDEN_CREDIT_POS)
            for U i = 0; i < len(hidden_credit); i += 1
                {PPUDATA}(hidden_credit[i])

    ppu_reset_scroll(0, scroll.a)

    {PPUMASK}(PPUMASK_ON)
    {PPUCTRL}(PPUCTRL_NMI_ON)

    if scroll != 0
        // wait until rendering hits the bottom of the logo
        ppu_await_status(PPUSTATUS_SPR_0)
        // turn off rendering to hide wrap around
        {PPUMASK}(0)

mode title(Bool scroll_logo)
: nmi title_nmi
    {PPUCTRL}(0)
    {PPUMASK}(0)

    // Set the palette:
    palette = palette_initial
    ppu_upload_palette()

    // Setup the nametable:
    ppu_reset_addr($2000)
    ppu_upload_rlz(@title_screen)
    if scroll_logo
        scroll = $4f
        sprite_y = 1
    else
        scroll = 0


    // avoid one frame of logo visible at the bottom
    //{PPUMASK}(PPUMASK_ON)
    {PPUCTRL}(PPUCTRL_NMI_ON)

    state(0)
    set_oam(0, $50, sprite_y.a, $50, 0)

    while true
        if scroll != 0
            scroll -= 0.5
            // move sprite 0 down to stay in sync with logo
            sprite_y += 0.5
            set_oam_y(0, sprite_y.a)
        else
            // either done scrolling or didn't scroll in the first place
            // make sure the sprite 0 isn't blocking anything
            hide_oam(0)

            // show the title screen text
            palette[5] = $22
            palette[8] = WHITE

            // the "Press start button" is set to use palette 3 so make it
            // blink by toggling between white and black
            if frame & 31 >= 16
                palette[11] = WHITE
            else
                palette[11] = BLACK

        update_pads()
        frame += 1
        if frame >= $200 || (pads[0].pressed & BUTTON_START)
            goto mode attract()
            : preserves

        nmi

mode game()

chrrom
    file(raw, "title.chr")
    file(raw, "title.chr")
    file(fmt, "opening1/opening1.chr")
    file(fmt, "opening1/opening1.chr")
    file(fmt, "opening2/opening2.chr")
    file(fmt, "opening2/opening2.chr")
    file(fmt, "opening3/opening3.chr")
    file(fmt, "opening3/opening3.chr")
