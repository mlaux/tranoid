charmap("0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ.,:?!\"'/()& \0", '\0', $d0)
: stows /strings

ct U[25] title_palette_initial = U[25](
    $2c, $27, $30,
    $0f, $0f, $0f, // make text black originally
    $0f, $0f, $0f,
    $0f, $0f, $0f,

    $0f, $0f, $0f,
    $0f, $0f, $0f,
    $0f, $0f, $0f,
    $0f, $0f, $0f,

    $0f // background color
)

ct U[] hidden_credit = U[] (
    $c0, $c1, $c2, $0, // NES
    $c3, $c9, $c4, $c5, $0, // PORT
    $c6, $c7, $0, // BY
    $c8, $c9, $c0, $c2, $c5, $c8, $ca, $c2, $c5, $0, // CONSTCAST
    $cb, $cc, $cb, $cd
) // 2025

data /rlz
    [] title_screen
        file(rlz, "title.nam")
    [] opening_1
        file(rlz, "opening1/opening1.nam")
    [] opening_2
        file(rlz, "opening2/opening2.nam")
    [] opening_3
        file(rlz, "opening3/opening3.nam")

data /strings
    [] opening_1_line_1
        ("TRAN IS A SPACE ALIEN GIRL")
    [] opening_1_line_2
        ("WHO CAME FROM THE TRANSTAR.")

    [] opening_2_line_1
        ("SHE WANDERS AROUND LOOKING FOR")
    [] opening_2_line_2
        ("TRANOID SPREAD TO THE")
    [] opening_2_line_3
        ("BIRTHPLACE.")

    [] opening_3_line_1
        ("DODGE THE ADVANCE OF")
    [] opening_3_line_2
        ("THE EVIL SPACE ALIENS AND")
    [] opening_3_line_3
        ("APPROACH THE MISTERY OF")
    [] opening_3_line_4
        ("TRANOID.")

ct UU[] opening_line_starts = UU[] ($2203, $2243, 0, $2201, $2246, $228b, 0, $2206, $2244, $2285, $22cc, 0)

ct CC/rlz[] opening_gfx = CC/rlz[] (@opening_1, @opening_2, @opening_3)

ct CC/strings[] opening_lines = CC/strings[] (
    @opening_1_line_1, @opening_1_line_2, 0,
    @opening_2_line_1, @opening_2_line_2, @opening_2_line_3, 0,
    @opening_3_line_1, @opening_3_line_2, @opening_3_line_3, @opening_3_line_4, 0
)

ct UU[] delays = UU[] ($100, $120, $180)

ct U[] opening_palettes1 = U[] ($12, $16, $22)
ct U[] opening_palettes2 = U[] ($2c, $1c, $15)
ct U[] opening_palettes3 = U[] ($32, $10, $32)

vars /title
    UF scroll = $4f
    UF sprite_y = 2
    Bool scrolling = true
    U attract_src_index = 0
    UU attract_dst_addr = opening_line_starts[0]
    U attract_line_char = 0
    UU frame = 0
    Bool attract_done_with_text = false

mode main()

    goto mode title() : preserves // nothing

nmi attract_nmi()
    if ready
        ppu_upload_oam_poll_pads(0)
        ppu_upload_palette()
        if !attract_done_with_text && frame & 3 == 0
            {PPUSTATUS}()
            {PPUADDR}(attract_dst_addr.b)
            {PPUADDR}(attract_dst_addr.a)
            {PPUDATA}(attract_line_char)

            attract_src_index += 1
            attract_dst_addr += 1

    ppu_reset_scroll(0, 0)
    {PPUMASK}(PPUMASK_ON)
    {PPUCTRL}(PPUCTRL_NMI_ON)

fn upload_attract_gfx(U num)
    {PPUCTRL}(0)
    {PPUMASK}(0)
    state(num + 1)

    palette[0] = opening_palettes1[num]
    palette[1] = opening_palettes2[num]
    palette[2] = opening_palettes3[num]
    ppu_upload_palette()

    ppu_reset_addr($2000)
    ppu_upload_rlz(opening_gfx[num])
    ppu_reset_scroll(0, 0)
    
    {PPUCTRL}(PPUCTRL_NMI_ON)

mode attract()
: nmi attract_nmi
    U attract_mode_scene = 0
    U attract_line_num = 0

    hide_oam(0)
    upload_attract_gfx(attract_mode_scene)

    while true
        // load next character
        if !attract_done_with_text
            attract_line_char = opening_lines[attract_line_num][attract_src_index]
            if attract_line_char == charmap.sentinel
                // end of string, try to move on to next
                attract_src_index = 0
                attract_line_num += 1
                attract_dst_addr = opening_line_starts[attract_line_num]
                if attract_dst_addr == 0
                    // all lines drawn
                    attract_done_with_text = true
                else
                    // do this again with first char of next line
                    attract_line_char = opening_lines[attract_line_num][attract_src_index]
        frame += 1
        if frame >= delays[attract_mode_scene]
            attract_mode_scene += 1
            if attract_mode_scene == 3
                goto mode title() : preserves
            attract_line_num += 1
            attract_src_index = 0
            attract_dst_addr = opening_line_starts[attract_line_num]
            attract_done_with_text = false
            attract_line_char = opening_lines[attract_line_num][attract_src_index]
            frame = 0
            upload_attract_gfx(attract_mode_scene)
        nmi

    // Animate the CHRROM:
    // U frame = 0
    // state(frame)
    // while true
    //     state(frame)
    //     frame += 1
    //     frame &= %11
    //     wait(8)

nmi title_nmi()
    if ready
        ppu_upload_oam_poll_pads(0)
        ppu_upload_palette()
    ppu_reset_scroll(0, scroll.a)

    {PPUMASK}(PPUMASK_ON)
    {PPUCTRL}(PPUCTRL_NMI_ON)

    if scrolling
        // wait until rendering hits the bottom of the logo
        ppu_await_status(PPUSTATUS_SPR_0)
        // move sprite 0 one pixel down to stay in sync with logo
        set_oam_y(0, sprite_y.a)
        sprite_y += 0.5
        // turn off rendering to hide wrap around
        {PPUMASK}(0)

mode title()
: nmi title_nmi
    {PPUCTRL}(0)
    {PPUMASK}(0)

    // Set the palette:
    palette = title_palette_initial
    ppu_upload_palette()

    // Setup the nametable:
    ppu_reset_addr($2000)
    ppu_upload_rlz(@title_screen)
    ppu_reset_scroll(0, scroll.a)

    // avoid one frame of logo visible at the bottom
    //{PPUMASK}(PPUMASK_ON)
    {PPUCTRL}(PPUCTRL_NMI_ON)

    state(0)
    set_oam(0, $50, sprite_y.a, $50, 0)

    while true
        if scrolling
            scroll -= 0.5
            if scroll == 0
                scrolling = false

                palette[0] = $2c
                palette[1] = $27
                palette[2] = $20

                palette[5] = $22
                palette[8] = $30

        frame += 1
        if frame >= $200
            goto mode attract()
            : preserves

        update_pads()
        if pads[0].pressed & BUTTON_START
            goto mode attract()
            : preserves

        nmi

mode game()

chrrom
    file(raw, "title.chr")
    file(raw, "title.chr")
    file(fmt, "opening1/opening1.chr")
    file(fmt, "opening1/opening1.chr")
    file(fmt, "opening2/opening2.chr")
    file(fmt, "opening2/opening2.chr")
    file(fmt, "opening3/opening3.chr")
    file(fmt, "opening3/opening3.chr")
