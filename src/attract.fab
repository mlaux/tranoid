// Attract mode

data /rlz
    [] opening_1
        file(rlz, "opening1/opening1.nam")
    [] opening_2
        file(rlz, "opening2/opening2.nam")
    [] opening_3
        file(rlz, "opening3/opening3.nam")

data /strings
    [] opening_1_line_1
        ("TRAN IS A SPACE ALIEN GIRL")
    [] opening_1_line_2
        ("WHO CAME FROM THE TRANSTAR.")

    [] opening_2_line_1
        ("SHE WANDERS AROUND LOOKING FOR")
    [] opening_2_line_2
        ("TRANOID SPREAD TO THE")
    [] opening_2_line_3
        ("BIRTHPLACE.")

    [] opening_3_line_1
        ("DODGE THE ADVANCE OF")
    [] opening_3_line_2
        ("THE EVIL SPACE ALIENS AND")
    [] opening_3_line_3
        ("APPROACH THE MISTERY OF")
    [] opening_3_line_4
        ("TRANOID.")

ct UU[] opening_line_starts = UU[] ($2203, $2243, 0, $2201, $2246, $228b, 0, $2206, $2244, $2285, $22cc, 0)

ct CC/rlz[] opening_gfx = CC/rlz[] (@opening_1, @opening_2, @opening_3)

ct CC/strings[] opening_lines = CC/strings[] (
    @opening_1_line_1, @opening_1_line_2, 0,
    @opening_2_line_1, @opening_2_line_2, @opening_2_line_3, 0,
    @opening_3_line_1, @opening_3_line_2, @opening_3_line_3, @opening_3_line_4, 0
)

ct U[] opening_palettes1 = U[] ($12, $16, $22)
ct U[] opening_palettes2 = U[] ($2c, $1c, $15)
ct U[] opening_palettes3 = U[] ($32, $10, $32)

ct UU[3] delays = UU[] ($1c0, $1c0, $1c0)

vars /attract
    U attract_src_index = 0
    UU attract_dst_addr = opening_line_starts[0]
    U attract_line_char = 0
    Bool attract_done_with_text = false

nmi attract_nmi()
    if ready
        ppu_upload_oam_poll_pads(0)
        ppu_upload_palette()
        {PPUSTATUS}()
        if !attract_done_with_text && frame & 3 == 0
            {PPUADDR}(attract_dst_addr.b)
            {PPUADDR}(attract_dst_addr.a)
            {PPUDATA}(attract_line_char)

            attract_src_index += 1
            attract_dst_addr += 1

    ppu_reset_scroll(0, 0)
    {PPUCTRL}(PPUCTRL_NMI_ON)
    {PPUMASK}(PPUMASK_ON | PPUMASK_NO_CLIP)

    puf.process(PUF_DEFAULT)

fn upload_attract_gfx(U num)
    {PPUCTRL}(0)
    {PPUMASK}(0)
    set_bank(num + 1)

    palette[0] = opening_palettes1[num]
    palette[1] = opening_palettes2[num]
    palette[2] = opening_palettes3[num]
    palette[8] = WHITE // in case they hit start before the title screen ends
    ppu_upload_palette()

    ppu_reset_addr($2000)
    ppu_upload_rlz(opening_gfx[num])
    ppu_reset_scroll(0, 0)

    {PPUCTRL}(PPUCTRL_NMI_ON)

mode attract()
: nmi attract_nmi
    frame = 0
    U attract_mode_scene = 0
    U attract_line_num = 0
    UU delay = 0

    hide_oam(0)
    upload_attract_gfx(attract_mode_scene)

    while true
        // load next character
        if !attract_done_with_text
            attract_line_char = opening_lines[attract_line_num][attract_src_index]
            if attract_line_char == charmap.sentinel
                // end of string, try to move on to next
                attract_src_index = 0
                attract_line_num += 1
                attract_dst_addr = opening_line_starts[attract_line_num]
                if attract_dst_addr != 0
                    // do this again with first char of next line
                    attract_line_char = opening_lines[attract_line_num][attract_src_index]
                else
                    // all lines drawn
                    attract_done_with_text = true

        delay += 1
        if delay >= delays[attract_mode_scene]
            delay = 0
            attract_mode_scene += 1
            if attract_mode_scene == 3
                goto mode title(true, false) : preserves /global
            attract_line_num += 1
            attract_src_index = 0
            attract_dst_addr = opening_line_starts[attract_line_num]
            attract_done_with_text = false
            attract_line_char = opening_lines[attract_line_num][attract_src_index]
            upload_attract_gfx(attract_mode_scene)

        // blink the press start text
        if frame & 31 >= 16
            palette[11] = WHITE
        else
            palette[11] = BLACK

        update_pads()
        if pads[0].pressed & BUTTON_START
            goto mode title(false, false)
            : preserves /global

        update_music_speed()
        frame += 1
        nmi